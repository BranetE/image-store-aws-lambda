plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

// Common configuration for all subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'org.example'
    version = '1.0-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'com.amazonaws:aws-lambda-java-core:1.2.2'
        implementation 'com.amazonaws:aws-lambda-java-events:3.11.1'
        implementation 'ch.qos.logback:logback-classic:1.4.14'
        implementation 'org.slf4j:slf4j-api:2.0.13'

        testImplementation 'org.mockito:mockito-core:5.18.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    shadowJar {
        archiveClassifier.set('')

        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/maven/**'
        exclude 'META-INF/gradle/**'

        mergeServiceFiles()
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}